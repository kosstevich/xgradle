/*
 * Copyright 2025 BaseALT Ltd
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.text.SimpleDateFormat
import java.util.Date
import java.util.Locale

plugins {
    application
    id("org.altlinux.xgradle-publishing-conventions")
}

dependencies {
    implementation(libs.bundles.maven.tooling)
    implementation(libs.bundles.logging)
    implementation(libs.jcommander)
    implementation(libs.plexus.utils)
    implementation(libs.guice)
    runtimeOnly(libs.bundles.guice.deps)
    testImplementation(platform(libs.junit.bom))
    testImplementation(libs.bundles.testing)

    shadow(libs.bundles.maven.tooling)
    shadow(libs.bundles.logging)
    shadow(libs.jcommander)
    shadow(libs.plexus.utils)
    shadow(libs.guice)
    shadow(libs.bundles.guice.deps)
}

val gitCommitId = createCommitIdProvider()
val buildTime = SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssZ", Locale.getDefault()).format(Date())

fun createCommitIdProvider(): Provider<String> {
    return providers.systemProperty("gitCommitId")
        .orElse(providers.provider {
            runGitCommand("rev-parse", "HEAD~0")
        })
        .orElse("<Unknown>")
        .map {it.trim()}
        .filter { it.isNotBlank() }
}

private fun runGitCommand(vararg args: String): String? {
    return try {
        val process = ProcessBuilder("git", *args)
            .directory(rootProject.projectDir)
            .redirectOutput(ProcessBuilder.Redirect.PIPE)
            .redirectError(ProcessBuilder.Redirect.DISCARD)
            .start()

        process.waitFor(10, TimeUnit.SECONDS)
        if (process.exitValue() == 0) {
            process.inputStream.bufferedReader().use {
                it.readText().trim()
            }
        } else {
            null
        }
    } catch (e: Exception) {
        null
    }
}

tasks.processResources {
    filesMatching("application.properties") {
        expand(
            "projectVersion" to project.version,
            "gitCommitHash" to gitCommitId.get(),
            "projectBuildTime" to buildTime
        )
    }
}

application {
    mainClass.set("${project.group}.impl.Main")
}

tasks.shadowJar {
    dependsOn(tasks.processResources)

    configurations = listOf(project.configurations.shadow.get())

    minimize()
    isEnableRelocation = true

    archiveBaseName.set(project.name)
    archiveClassifier.set(null as String?)

    manifest{
        attributes(
            "Main-Class" to "${project.group}.impl.Main",
            "Implementation-Version" to project.version
        )
    }
}

tasks.named<Jar>("jar") {
    enabled = false
}

tasks.register<Jar>("sourcesJar") {
    archiveBaseName.set(project.name)
    archiveClassifier.set("sources")
    from(sourceSets.main.get().allSource)
}

tasks.register<Jar>("javadocJar") {
    archiveBaseName.set(project.name)
    archiveClassifier.set("javadoc")
    from(tasks.javadoc)
}

tasks.withType<Javadoc>().configureEach {
    (options as StandardJavadocDocletOptions).addStringOption("Xdoclint:none", "-quiet")
}

tasks.register<Task>("createShellScript") {
    dependsOn("shadowJar")

    val outputDir = layout.buildDirectory.dir("dist")
    val scriptFile = outputDir.get().file(project.name).asFile

    outputs.file(scriptFile)

    doLast {
        val scriptContent = """
            #!/bin/sh
            target="${'$'}0"
            while [ -L "${'$'}target" ]; do
                link=$(readlink "${'$'}target") || exit 1
                case "${'$'}link" in
                    /*) target="${'$'}link" ;;
                    *)  target=$(dirname "${'$'}target")/"${'$'}link" ;;
                esac
            done
            DIR=$(cd "$(dirname "${'$'}target")" && pwd)
            exec java -jar "${'$'}DIR/${project.name}.jar" "${'$'}@"
        """.trimIndent()

        scriptFile.parentFile.mkdirs()
        scriptFile.writeText(scriptContent)
        scriptFile.setExecutable(true)
    }
}

tasks.matching { it.name in listOf("distZip", "distTar", "startScripts") }
    .configureEach {
        dependsOn(tasks.named("shadowJar"))
    }

xgradlePublishingConventions {
    projectName.set("XGradle Tool")
    projectDescription.set("${rootProject.name} support tool")
    projectUrl.set("https://altlinux.space/ALTLinux/xgradle.git")

    licenseName.set("The Apache License, Version 2.0")
    licenseUrl.set("https://www.apache.org/licenses/LICENSE-2.0")

    enableCopyPublications.set(true)

    withShadowJar()
    withSourcesJar()
    withJavadocJar()

    developer("xeno", "Ivan Khanas", "xeno@altlinux.org")
}

tasks.named("publishToMavenLocal") {
    dependsOn("createShellScript")
}

tasks.named("build") {
    dependsOn("createShellScript")
}

tasks.test {
    useJUnitPlatform()
}